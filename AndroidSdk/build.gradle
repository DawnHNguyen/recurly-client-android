plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven'
    id 'signing'
}

// NOTE: update RecurlyApi#VERSION as well
version = "2.0.0"
group = "com.recurly"
archivesBaseName = "android-sdk"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    viewBinding {
        enabled = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'com.google.android.material:material:1.4.0'

    //Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    // LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    // Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
}

if (project.hasProperty("sonatypeUsername")){
uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Recurly Android SDK'
            packaging 'aar'
            description 'Android SDK for Recurly payment services'
            url 'https://www.recurly.com'

            scm {
                url 'https://github.com/recurly/recurly-client-android'
                connection 'scm:https://github.com/recurly/recurly-client-android.git'
                developerConnection 'scm:git@github.com:recurly/recurly-client-android.git'
            }

            issueManagement {
                system 'GitHub'
                url 'https://github.com/recurly/recurly-client-android/issues'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'support'
                    name 'Recurly Support'
                    email 'support@recurly.com'
                }
            }
        }
     }
  }
}
